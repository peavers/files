plugins {
	id 'org.springframework.boot' version '2.4.1'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'com.diffplug.spotless' version '5.9.0'
	id 'com.google.cloud.tools.jib' version '2.7.1' apply false
	id 'application'
	id 'java'
}

repositories {
	mavenCentral()
}

mainClassName = 'space.forloop.files.FilesApplication'

jar {
	enabled = false
}

def javaProjects = [
		'autotools-app',
		'autotools-common',
		'autotools-data',

		'rule-copy-media',
		'rule-delete-directories',
		'rule-delete-files',
		'rule-duplicate-media'
]

javaProjects.each {
	name ->

		project(":$name") {
			apply plugin: 'org.springframework.boot'
			apply plugin: 'io.spring.dependency-management'
			apply plugin: 'com.diffplug.spotless'
			apply plugin: 'java'

			group = 'com.codacy.'
			version = '0.0.1-SNAPSHOT'
			sourceCompatibility = '11'

			repositories {
				mavenCentral()
				maven {
					url "https://repo.microstream.one/repository/maven-public"
				}
			}

			ext {
				set('springCloudVersion', "Hoxton.SR6")
			}

			dependencies {
				// Spring
				implementation 'org.springframework.boot:spring-boot-starter-webflux'

				// Apache
				implementation 'org.apache.commons:commons-lang3:3.11'
				implementation 'commons-io:commons-io:2.8.0'

				// Google
				implementation 'com.google.guava:guava:30.1-jre'

				// Other
				implementation 'one.util:streamex:0.7.3'

				// Dev
				compileOnly 'org.projectlombok:lombok'
				annotationProcessor 'org.projectlombok:lombok'
				annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
			}

			spotless {
				java {
					licenseHeader '/* Licensed under Apache-2.0 */'
					importOrder 'java', 'javax', 'org', 'com', ''
					removeUnusedImports()
					googleJavaFormat()
				}
			}

			dependencyManagement {
				imports {
					mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
				}
			}

			build.dependsOn spotlessJavaApply
		}
}
